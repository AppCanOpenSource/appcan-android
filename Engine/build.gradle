buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
import proguard.gradle.ProGuardTask

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion '23.0.2'
    packagingOptions {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets {

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId 'org.zywx.wbpalmstar.widgetone.uex'
        minSdkVersion 14
        targetSdkVersion 19 //
        //    multiDexEnabled true
    }
    productFlavors {
        crosswalk {
//            sourceSets.main.java.srcDirs = ['src/crosswalk/java','src/main/java']
        }
        webview {
//            sourceSets.main.java.srcDirs = ['src/main/java','src/webview/java']
        }
        x5 {
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFile 'proguard.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    crosswalkCompile(name: 'crosswalk-19.49.514.0', ext: 'aar')
    crosswalkCompile fileTree(dir: 'crosswalk/libs', include: '*.jar')
    webviewCompile files('libs/libacedes-v1.jar')
    compile files('libs/aceimageloader.jar')
    compile files('libs/android-support-v4.jar')
    compile files('libs/commons-io-2.4.jar')
    compile files('libs/gson-2.2.4.jar')
    compile files('libs/httpmime-4.1.3.jar')
    webviewCompile files('libs/libacedes-v1.jar')
    crosswalkCompile files('libs/libacedes_for_crosswalk.jar')
    compile files('libs/wmqtt.jar')
}


/**--------------------------------------以下为导出jar部分--------------------------------------------**/

ext {
    // 内核版本，默认为webview
    kernel = "crosswalk"
}

println "kernel is " + kernel

//排除buildConfig,这里的路径应该设置与包名一致
def buildConfigPath = "org/zywx/wbpalmstar/widgetone/uex/"

def jarBaseName="AppCanEngine_un_proguard"

task clearJar(type: Delete) {
    delete 'build/outputs/jar/'+jarBaseName
}

String kernelName=kernel
def javacTaskName="compile"+kernelName.charAt(0).toUpperCase()+ kernelName.substring(1)+"ReleaseJavaWithJavac"

task makeJar(type:Jar, dependsOn: [javacTaskName]) {
    //指定生成的jar名
    baseName jarBaseName
    //从哪里打包class文件
    from('build/intermediates/classes/'+kernel+'/release/')
    //打包到jar后的目录结构
    into('')
    destinationDir = file('build/outputs/jar')
    exclude('**/R.class')
    exclude('**/R\$*.class')
    exclude('**/BuildConfig.class')
    exclude(buildConfigPath)
}

makeJar.dependsOn(clearJar)

def androidSDKDir = plugins.getPlugin('com.android.application').sdkHandler.getSdkFolder()
def androidJarDir = androidSDKDir.toString() + '/platforms/' + "${android.compileSdkVersion}" + '/android.jar'

task proguardEngineJar(type: ProGuardTask, dependsOn: ['makeJar']) {
    injars('build/outputs/jar/AppCanEngine_un_proguard.jar')
    outjars('build/outputs/jar/AppCanEngine.jar')
    libraryjars(androidJarDir)
    if(kernel=='crosswalk'){
        libraryjars('libs/crosswalk-19.49.514.0.aar')
    }
    configuration 'proguard.pro'
}